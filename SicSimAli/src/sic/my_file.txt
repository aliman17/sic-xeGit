. Parameter podamo v spr. N
. Rezultat dobimo v A
.
. FAKTORIELA
FAKT	START	0
FIRST	JSUB	INIT		. INICIALIZIRAMO SKLAD
		LDA		N			. NALOZIMO VREDNOST
		JSUB	SFAKT		. SUBRUTINA
HALT	J		HALT		. REZULTAT BO V A-JU
N		WORD	5
POINTER	WORD	0
. __________________GLAVNI PROGRAM__________________
SFAKT	COMP	#1			. ROBNI POGOJ
		JEQ		IZHODG		. SKOK, ČE KONEC
		STA		TMP			. SHRAMO ZACASNO REG.A
		RMO		L,A			. L->A
		JSUB	PUSHA		. PUSHAMO REG.L, DA GA NE ZGUBIMO
		LDA		TMP			. NAZAJ DOBIMO VREDNOST A-JA
		JSUB	PUSHA		. PUSHAMO A
		SUB		#1			. A-1 -> V REKURZIJO
		JSUB	SFAKT		. REKURZIJA
		STA		TMP			. SHRANIMO REKURZIVNO PORAČUNANO ZADEVO
		JSUB	POPA		. PRIDOBI N
		MUL		TMP			. N*(N-1)!
		STA		TMP			. SHRANI REZULTAT
		JSUB	POPA		. PRIDOBI L
		RMO		A,L			. A->L
		LDA		TMP			. V A DAMO REZULTAT
IZHODG	RSUB
TMP		WORD	0			. ZA KARKOLI LAHKO UPORABIMO
. __________________STACK INIT______________________
INIT	STA		TMP_A1		. SHRANI REG. A, DA GA NE POKVARIMO
		LDA		#STACK		. POINTER NA STACK
		STA		POINTER		. INICIALIZIRAMO STACK POINTER
		LDA		TMP_A1		. OBNOVIMO REG. A
		RSUB				. VRNITEV
TMP_A1	WORD	0
. __________________PUSH____________________________
PUSHA	STA		@POINTER	. SHRANI REG. A
		STA		TMP_A2		. DA BOMO OBNOVILI A
		LDA		POINTER
		ADD		#3			. POVEČAMO SKLADOVNI KAZALEC
		STA		POINTER
		LDA		TMP_A2		. OBNOVIMO REG.A
		RSUB
TMP_A2	WORD	0
. __________________POP_____________________________
POPA	LDA		@POINTER
		LDA		POINTER		. REG.A LAHKO POPACKAMO, SAJ BOMO POTEM POP-ALI
		SUB		#3
		STA		POINTER		. ZMANJSAMO SKLADOVNI KAZALEC
		LDA		@POINTER	. V REG.A NALOZIM KAR JE NA SKLADU -> POP-AMO
		RSUB
TMP_A3	WORD	0
		END		FIRST
STACK	RESW	50